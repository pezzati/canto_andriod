apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

def superpowered_sdk_path = new File(projectDir, '../Superpowered')
def ffmpeg_path = new File(projectDir, './jniLibs')


android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.hmomeni.canto"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_ARM_NEON=TRUE', '-DANDROID_STL=c++_static', "-DPATH_TO_SUPERPOWERED:STRING=${superpowered_sdk_path}", "-DPATH_TO_FFMPEG:STRING=${ffmpeg_path}"
                cFlags '-O3', '-fsigned-char' // full optimization, char data type is signed
                cppFlags '-fsigned-char', "-I${superpowered_sdk_path}", "-I${ffmpeg_path}"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "Canto"
            def SEP = "-"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName
            if (buildType == "debug") {
                buildType = "-" + buildType
            } else {
                buildType = ""
            }

            def newApkName = project + SEP + "v" + version + buildType + ".apk"

            output.outputFileName = newApkName
        }
    }

//    sourceSets.main.jniLibs.srcDirs = ['./jniLibs/']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:recyclerview-v7:27.1.1"
    implementation "com.android.support:cardview-v7:27.1.1"
    implementation "com.android.support:design:27.1.1"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    implementation "android.arch.lifecycle:reactivestreams:1.1.1"

    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.4.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    implementation 'jp.wasabeef:glide-transformations:3.3.0'


    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha1'

    implementation "com.google.dagger:dagger:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha07"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha07"

    implementation 'com.karumi:dexter:5.0.0'
    implementation 'com.github.angads25:filepicker:1.1.1'

    implementation 'com.pixplicity.easyprefs:library:1.8.1@aar'
    implementation('com.alibaba.android:ultraviewpager:1.0.7.7@aar') {
        transitive = true
    }

    implementation 'nl.bravobit:android-ffmpeg:1.1.5'
}
